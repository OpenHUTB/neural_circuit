   /* horizontal cell dendrite, with spine and soma. */
      
   drm = 5000;

   pre = 100;                           /* convenient node definitions */
   spine1 = 10;
   spine2 = 20;

   numsegs = 4;                         /* number of segments */
   seglen = 60;                         /* lengths of segments */ 
   spinelen = 5;
   totlen = numsegs * seglen + 2 * spinelen;
   soma = 5;                            /* soma is connected to node 5 */ 

   at pre sphere dia 2;
   conn pre to spine1 synapse expon 5 thresh -.05 maxcond 2e-9;

   conn spine1 to 1         cable length spinelen dia 0.1;
   conn spine2 to numsegs+2 cable length spinelen dia 0.1;

   for (i=1; i<=numsegs; i++)
        conn i to i+1 cable length seglen dia 1;
   conn i to i+1 cable length seglen dia 1;
   
   at soma sphere dia 20;

   stim node pre vclamp -0.01 start 0 dur 0.01;

   if (disp) {

     at soma   loc (0,0);
     for (i=1; i<=numsegs; i++)
          at i loc (-(numsegs-i+1)*seglen,0);
     at numsegs+2 loc (seglen,0);
     at pre    loc (-numsegs*seglen,10);
     at spine1 loc (-numsegs*seglen,5);
     at spine2 loc (seglen,5);

     display center (0,0,0);
     display size (500);
     display matching [-1][-1][-1];
     // if (disp & 8) display node matching 5;
     if (disp & 8) display node matching [-1][-1][-1] dscale 2 color 5;
     exit;
   };

   step .01;                                 /* run model for 10 msec */

   graph X max totlen min 0;                 /* commands to scale graph */
   graph Y max 0 min -.07;
   graph init;

   graph pen (12);
   graph (0, V[spine1]);                     /* post-synaptic potential */
   for (i=0; i<=numsegs; i++) {
      if (i==1) graph pen (2);
      graph (i*seglen+spinelen, V[i+1]);     /* graph volts vs. distance */
   };
   graph pen (1);
   graph (totlen, V[spine2]);                /* output signal at spine 2*/    

   gpen (7);                                 /* labels for graph */
   gmove (0.02,0.85);
   gtext ("Volts");
   gmove (0.45,0.01);
   gtext ("microns");
